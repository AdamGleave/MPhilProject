(max6.cfg (C) 2009-2013, Matt Mahoney, Mike Russell
Licensed under GPL v3. http://www.gnu.org/copyleft/gpl.html)

comp 5 9 0 0 31 (hh hm ph pm n)
  0 const 158 (context chain)
  1 icm 5
  2 isse 12 1
  3 isse 17 2
  4 isse $3+18 3
  5 icm $3+19
  6 mix $3+20 0 6 49 255
  7 isse $3+19 6
  8 isse $3+19 7
  9 isse $3+20 8
  10 isse $3+20 9
  11 icm $3+20
  12 match $3+22 $3+23
  13 icm $3+18 (word order 0)
  14 isse $3+20 13 (word order 0+byte)
  15 icm 9
  16 icm 9
  17 mix 16 0 17 13 255
  18 icm 12
  19 icm 15
  20 isse 16 19
  21 icm 12
  22 isse 16 21
  23 isse 18 22
  24 mix 17 0 24 6 255
  25 mix 8 0 24 41 255
  26 mix2 0 24 25 73 0
  27 sse 8 26 8 255
  28 mix2 8 26 27 36 255
  29 sse 22 28 17 255
  30 mix2 0 28 29 85 0
hcomp
  c++ *c=a b=c a=0 (save in rotating buffer)
  d= 2 hash *d=a hash
  d++ b-- hash *d=a
  d++ b-- hash *d=a
  d++ b-- hash *d=a
  d++ b-- hash hash *d=a (mix)
  d++ hash *d=a
  d++ b-- hash *d=a
  d++ b-- hash b-- hash *d=a
  d++ b-- hash b-- hash *d=a
  d++ b-- hash b-- hash *d=a
  d++ b-- hash *d=a
  d++ a=*c a&~ 32 a-= 65 a&= 255 (13: order 0 words)
  a> 25 if
    a=*d a== 0 ifnot
      d++ *d=a d--
    endif
    *d=0
  else
    d++ hashd d-- a+=*d hashd
  endif
  d++ (14)
  d++ b=c hash *d=a (15: order 1)
  d++ hash *d=a (16: another order 1)
  d++ b-- hash *d=a (17: mix)
  d++ a=0 hash *d=a (18: sparse order 1 context)
  d++ a=*c a== 10 if (19: order 1 column model in R1)
      a=0 r=a 1
    else
      a=r 1 a< 64 if a++ endif r=a 1
    endif
    b=c hash *d=a
  d++ b-- hash *d=a (20: order 2 column model)
  d++ (bracket model [[...]])
      a=*c a== 91 if a=r 2 a< 2 if a++ r=a 2 endif endif
      a== 93 if a=r 2 a> 0 if a-- r=a 2 endif endif
      b=c a=r 2 hash *d=a
  d++ b-- hash *d=a
  d++ b-- hash *d=a
  d++ a=*c a<<= 8 *d=a (mix)
  d++ a=*c a<<= 14 *d=a (mix)
  d++ (mix2)
  d++ (sse)
  d++ (mix2)
  d++ hash *d=a (sse)
  halt
post
  0
end
